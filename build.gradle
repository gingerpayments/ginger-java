plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

group = 'com.gingerpayments'
version = '2.1.1'
archivesBaseName = project.name

repositories {
    mavenCentral()
}

sourceSets {
    examples {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    examplesImplementation.extendsFrom implementation
    examplesRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api 'com.squareup.okhttp3:okhttp:4.9.1'
    api 'org.json:json:20201115'

    testImplementation 'junit:junit:4.13.1'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.9.1")

    examplesImplementation 'junit:junit:4.13.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

task examples(type: Test) {
    description = 'Runs examples.'

    inputs.property 'GINGER_ENDPOINT', System.getenv().get("GINGER_ENDPOINT")
    inputs.property 'GINGER_API_KEY', System.getenv().get("GINGER_API_KEY")

    testLogging {
        events "standardOut", "standardError"
        outputs.upToDateWhen { false }
    }

    testClassesDirs = sourceSets.examples.output.classesDirs
    classpath = sourceSets.examples.runtimeClasspath
    shouldRunAfter test
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name = project.group + ':' + project.name
                packaging = 'jar'
                description = 'Java SDK for connecting to APIs managed by Ginger Payments.'
                url = 'https://github.com/gingerpayments/ginger-java'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        organization = 'Ginger'
                        organizationUrl = 'https://www.gingerpayments.com/'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/gingerpayments/ginger-java.git'
                    developerConnection = 'scm:git:ssh://github.com:gingerpayments/ginger-java.git'
                    url = 'https://github.com/gingerpayments/ginger-java/tree/master'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign configurations.archives
}
